class Game{
    field Person person;
    field Box box;

    constructor Game new(){
        let person=Person.new(3,4);
        let box=Box.new(10,13);
        return this;
    } 

    method void dispose(){
        do person.dispose();
        do box.dispose();
        do Memory.deAlloc(this);
        return;
    }
    method void run(){
        var char key;
        var boolean exit;
        let exit=false;

        while (~exit){
            let key = Keyboard.keyPressed();
            if (key = 81) { let exit = true; }     // q key
            //person up
            if (key = 131) { 
                if (((person.getY() - 1) = box.getY()) & (person.getX() = box.getX()) ) {
                    if (~(box.getY() = 0)) {
                        do box.moveUp();
                        do person.moveUp();
                    }
                }
                else {
                    do person.moveUp();
                } 
            } 
            //person down
            if (key = 133) { 
                if (((person.getY() + 1) = box.getY()) & (person.getX() = box.getX()) ) {
                    if (~(box.getY() = 15)) {
                        do box.moveDown();
                        do person.moveDown();
                    }
                }
                else {
                    do person.moveDown(); 
                }
            }
            //person left
            if (key = 130) {
                if (((person.getX() - 1) = box.getX()) & (person.getY() = box.getY()) ) {
                    if (~(box.getX() = 0)) {
                        do box.moveLeft();
                        do person.moveLeft();
                    }
                }
                else {
                    do person.moveLeft();
                }
            }  
            //person right
            if (key = 132) { 
                if (((person.getX() + 1) = box.getX()) & (person.getY() = box.getY()) ) {
                    if (~(box.getX() = 31)) {
                        do box.moveRight();
                        do person.moveRight();
                    }
                }
                else {
                    do person.moveRight(); 
                }
            }   
            //wait for the key to be released
            while(~(key=0)){
                let key = Keyboard.keyPressed();
            }
        }
        return;
    }
}