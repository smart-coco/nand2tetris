// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    Mux16(a=instruction,b=ALUoutput,sel=instruction[15],out=Mux1out);
    Mux16(a=ARegisterout,b=inM,sel=instruction[12],out=Mux2out);
    //writeM
    And(a=instruction[15],b=instruction[3],out=writeM);
    //ARegister
    Not(in=instruction[15],out=ta1);
    Or(a=ta1,b=instruction[5],out=ta2);
    ARegister(in=Mux1out,load=ta2,out[0..14]=addressM,out=ARegisterout);
    //DRegister
    And(a=instruction[15],b=instruction[4],out=td);
    DRegister(in=ALUoutput,load=td,out=DRegisterout);
    // PC
    And(a=instruction[2],b=instruction[15],out=j1);
    And(a=instruction[1],b=instruction[15],out=j2);
    And(a=instruction[0],b=instruction[15],out=j3);    
    And(a=true,b=ng,out=t1);
    And(a=true,b=zr,out=t2);
    Not(in=ng,out=nng);
    Not(in=zr,out=nzr);
    And(a=true,b=nng,out=t31);
    And(a=t31,b=nzr,out=t3);
    Mux(a=false,b=t1,sel=j1,out=t4);
    Mux(a=false,b=t2,sel=j2,out=t5);
    Mux(a=false,b=t3,sel=j3,out=t6);
    Or(a=t4,b=t5,out=t7);
    Or(a=t7,b=t6,out=fpc);
    PC(in=ARegisterout,load=fpc,inc=true,reset=reset,out[0..14]=pc);
    //ALU
    ALU(x=DRegisterout,y=Mux2out,zx=instruction[11],nx=instruction[10],zy=instruction[9],ny=instruction[8],f=instruction[7],no=instruction[6],out=outM,out=ALUoutput,zr=zr,ng=ng);
}